// Top-level build file where you can add configuration options common to all sub-projects/modules.
import dependencies.Dep
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

apply from: file('gradleScript/dependencyGraph.gradle')


buildscript {

    ext {
        isCi = System.getenv("CI") == "true"
        isReleaseBuild = System.getenv("RELEASE_BUILD") == "true"
        flutterUrl = "${rootProject.getProjectDir().parentFile.toString()}" + "/shared/flutter/build/host/outputs/repo"
        kotlin_version = '1.3.72'
    }

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath Dep.GradlePlugin.android
        classpath Dep.GradlePlugin.kotlin

    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    tasks.matching { it instanceof Test }.all {
        testLogging {
            events = ["failed", "skipped"]
            outputs.upToDateWhen { false }
            showStandardStreams = true
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xmaxerrs" << "500"
        }
        tasks.withType(KotlinCompile).all {
            kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }
}

subprojects {
    afterEvaluate {
        if (project.hasProperty('kapt')) {
            kapt {
                javacOptions {
                    option("-source", "8")
                    option("-target", "8")
                    option("-Adagger.fastInit=enabled")
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}